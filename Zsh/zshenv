#
# Defines environment variables.
#
# Authors:
#   Tim Millican <tim.millican@linux.com>
#

# Paths {{{
typeset -gU cdpath fpath mailpath path

# Set the the list of directories that cd searches.
cdpath=(
   $HOME
   $HOME/Projects
   $cdpath
)
# Remove non-existent directories
cdpath=($^cdpath(N-/))

# Set the list of directories that Zsh searches for programs.
path=(
  ${HOME}/.local/bin
  ${HOME}/bin
  /usr/local/{bin,sbin}
  /usr/{bin,sbin}
  /{bin,sbin}
  $path
)
# Remove non-existent directories
path=($^path(N-/))
# }}}
# Editors {{{
if (( $+commands[nvim] )); then
    export EDITOR=${commands[nvim]}
    export VISUAL=${commands[nvim]}
elif (( $+commands[vim] )); then
    export EDITOR=${commands[vim]}
    export VISUAL=${commands[vim]}
fi
# }}}
# Make sure that SSH and GPG agents are accessible {{{
if ! ssh-add -l &> /dev/null; then
    if [[ -f ${HOME}/.ssh-agent.env ]]; then
        source ${HOME}/.ssh-agent.env &> /dev/null
        if ! ssh-add -l &> /dev/null; then
            pkill -9 ssh-agent
            eval $(ssh-agent | tee ${HOME}/.ssh-agent.env) > /dev/null
        fi
    else
        pkill -9 ssh-agent
        eval $(ssh-agent | tee ${HOME}/.ssh-agent.env) > /dev/null
    fi
fi
if [[ -z $GPG_AGENT_INFO ]]; then
    if [[ -f ${HOME}/.gpg-agent.env ]]; then
        source ${HOME}/.gpg-agent.env
    else
        eval $(gpg-agent --daemon --write-env-file "${HOME}/.gpg-agent.env")
    fi
fi
# }}}
# EC2 Tools {{{
if [[ -f $HOME/.aws_keys ]]; then
    export AWS_ACCESS_KEY=$(cat $HOME/.aws_keys|head -n1)
    export AWS_SECRET_KEY=$(cat $HOME/.aws_keys|tail -n1)
fi
if [[ -d $HOME/.local/ec2-tools ]];then
    export EC2_HOME=$HOME/.local/ec2-tools
    export JAVA_HOME=$(print -l /usr/lib/jvm/java-*-openjdk-*/jre|sort|tail -n1)
    export PATH=$PATH:$EC2_HOME/bin
fi
# }}}
# Extra {{{
export NNTPSERVER="nntp.aioe.org"
# }}}
# Read local configuration {{{
if [[ -f ${ZDOTDIR:-$HOME}/.zshenv.local ]]; then
    source ${ZDOTDIR:-$HOME}/.zshenv.local
fi
# }}}

# vim: fdm=marker syntax=zsh
