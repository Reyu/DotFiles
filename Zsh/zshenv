#
# Defines environment variables.
#
# Authors:
#   Tim Millican <tim.millican@linux.com>
#

# Paths {{{
typeset -gU cdpath fpath mailpath path

# Set the the list of directories that cd searches.
cdpath=(
   $HOME
   $HOME/Projects
   $cdpath
)
# Remove non-existent directories
cdpath=($^cdpath(N-/))

# Set the list of directories that Zsh searches for programs.
path=(
  ${HOME}/.local/bin
  ${HOME}/bin
  /usr/local/{bin,sbin}
  /usr/{bin,sbin}
  /{bin,sbin}
  $path
)
# Remove non-existent directories
path=($^path(N-/))

# Python VirtualEnv Wrapper
export VIRTUAL_ENV_DISABLE_PROMPT=1
export WORKON_HOME=~/Projects/.python_virtual_environments
# }}}
# Editors {{{
if (( $+commands[nvim] )); then
    export EDITOR=${commands[nvim]}
    export VISUAL=${commands[nvim]}
elif (( $+commands[vim] )); then
    export EDITOR=${commands[vim]}
    export VISUAL=${commands[vim]}
fi
# }}}
# Make sure that SSH and GPG agents are accessible {{{
RUNNING_AGENT_PID="$(pgrep -u ${UID} ssh-agent)"
if [[ -z "${SSH_AGENT_PID}" || -z "${SSH_AUTH_SOCK}" || "${SSH_AGENT_PID}" != "${RUNNING_AGENT_PID}" ]]; then
    unset SSH_AGENT_PID SSH_AUTH_SOCK
    if [[ -r ${HOME}/.ssh-agent.env ]]; then
        source ${HOME}/.ssh-agent.env
        if [[ "${SSH_AGENT_PID}" != "${RUNNING_AGENT_PID}" ]]; then
            rm ${HOME}/.ssh-agent.env
            unset SSH_AGENT_PID SSH_AUTH_SOCK
            pkill -u ${UID} ssh-agent
            eval $(ssh-agent|grep -v echo|tee ${HOME}/.ssh-agent.env)
        fi
    else
        rm -f ${HOME}/.ssh-agent.env
        pkill -u ${UID} ssh-agent
        eval $(ssh-agent|grep -v echo|tee ${HOME}/.ssh-agent.env)
    fi
fi
unset RUNNING_AGENT_PID

gpg-connect-agent /bye
export GPG_TTY=$(tty)
# }}}
# EC2 Tools {{{
if [[ -f $HOME/.aws_keys ]]; then
    export AWS_ACCESS_KEY=$(cat $HOME/.aws_keys|head -n1)
    export AWS_SECRET_KEY=$(cat $HOME/.aws_keys|tail -n1)
fi
if [[ -d $HOME/.local/ec2-tools ]];then
    export EC2_HOME=$HOME/.local/ec2-tools
    export JAVA_HOME=$(print -l /usr/lib/jvm/java-*-openjdk-*/jre|sort|tail -n1)
    export PATH=$PATH:$EC2_HOME/bin
fi
# }}}
# Extra {{{
export NNTPSERVER="nntp.aioe.org"
# }}}
# Read local configuration {{{
if [[ -f ${ZDOTDIR:-$HOME}/.zshenv.local ]]; then
    source ${ZDOTDIR:-$HOME}/.zshenv.local
fi
# }}}

# vim: fdm=marker syntax=zsh
